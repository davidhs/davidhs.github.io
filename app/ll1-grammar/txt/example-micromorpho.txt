Program -> _ProgramCore

_ProgramCore -> Fundecl ; _ProgramCoreCont
_ProgramCore -> Vardecl ; _ProgramCoreCont
_ProgramCore -> Expr    ; _ProgramCoreCont

_ProgramCoreCont -> Fundecl ; _ProgramCoreCont
_ProgramCoreCont -> Vardecl ; _ProgramCoreCont
_ProgramCoreCont -> Expr    ; _ProgramCoreCont
_ProgramCoreCont -> #

Fundecl -> fun name ( _OptNameSeq ) Body

_OptNameSeq -> _NameSeq
_OptNameSeq -> #

_NameSeq     -> name _NameSeqCont
_NameSeqCont -> name _NameSeqCont
_NameSeqCont -> #

Vardecl -> var _NameOptAssignSeq

_NameOptAssignSeq     ->   name _OptAssign _NameOptAssignSeqCont
_NameOptAssignSeqCont -> , name _OptAssign _NameOptAssignSeqCont 
_NameOptAssignSeqCont -> #

_OptAssign -> _Assign
_OptAssing -> #

_Assign -> = Expr

Expr -> return Expr
Expr -> name _Assign
Expr -> Orexpr

Orexpr -> _AndexprSeq

_AndexprSeq     ->    Andexpr _AndexprSeqCont
_AndexprSeqCont -> || Andexpr _AndexprSeqCont
_AndexprSeqCont -> #

Andexpr -> _NotexprSeq

_NotexprSeq     ->    Notexpr _NotexprSeqCont
_NotexprSeqCont -> && Notexpr _NotexprSeqCont
_NotexprSeqCont -> #

Notexpr -> ! Notexpr
Notexpr -> Opexpr

Opexpr -> opname
Opexpr -> Smallexpr

Smallexpr -> name _SmallexprName
Smallexpr -> opname Expr
Smallexpr -> literal
Smallexpr -> ( Expr )
Smallexpr -> Ifexpr
Smallexpr -> while ( Expr ) Body
Smallexpr -> fun ( _OptNameSeq ) Body

_SmallexprName -> #
_SmallexprName -> ( _OptExprSeq )

_OptExprSeq -> _ExprSeq
_OptExprSeq -> #

_ExprSeq     ->   Expr _ExprSeqCont
_ExprSeqCont -> , Expr _ExprSeqCont
_ExprSeqCont -> #

Ifexpr -> if ( Expr ) Body _OptElsifSeq _OptElse

_OptElsifSeq -> _ElsifSeq
_OptElsifSeq -> #

_ElsifSeq     -> elseif ( Expr ) Body _ElsifSeqCont
_ElsifSeqCont -> elseif ( Expr ) Body _ElsifSeqCont
_ElsifSeqCont -> #

_OptElse -> else Body
_OptElse -> #

Body -> { _ProgramCore }
